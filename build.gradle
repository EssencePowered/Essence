buildscript {
    repositories{
        mavenCentral()
        maven { url "https://jitpack.io" }
    }
    dependencies {
        classpath 'com.github.qixalite:SpongeStart:1.4'
    }
}

plugins {
    id 'java'
    id 'idea'
    id 'eclipse'
    id 'com.github.hierynomus.license' version '0.12.1'
    id 'net.ellune.blossom' version '1.0.1'
}

ext.spongeapi = '3.1.0-SNAPSHOT'
ext.descriptions = 'Nucleus is a Sponge plugin that allows you to quickly start your server with essential commands, events, and other tidbits that you might need. '
ext.url = 'http://nucleuspowered.github.io'

group 'io.github.nucleuspowered'
version '0.1.0-SNAPSHOT'

apply plugin: 'SpongeStart'
apply plugin: 'net.ellune.blossom'

defaultTasks 'licenseFormat'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
    maven {
        name 'Sponge maven repo'
        url 'http://repo.spongepowered.org/maven'
    }
}

dependencies {
    compile "org.spongepowered:spongeapi:3.1.0-SNAPSHOT"

    testCompile "junit:junit:4.12"
    testCompile "org.mockito:mockito-all:1.10.19"
    testCompile "org.powermock:powermock-module-junit4:1.6.4"
    testCompile "org.powermock:powermock-api-mockito:1.6.4"
}

license {
    ext.name = project.name

    exclude "**/*.info"
    exclude "assets/**"
    exclude "*.properties"

    header file('HEADER.txt')
    sourceSets = project.sourceSets

    ignoreFailures false
    strictCheck true

    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}

sponge{
    eula true
}

blossom {
    def location = 'src/main/java/io/github/nucleuspowered/nucleus/PluginInfo.java'

    // replaceToken '@id@', project.name.toLowerCase(), location
    replaceToken '@name@', project.name, location
    replaceToken '@version@', project.version, location
    replaceToken '@informativeVersion@', project.version + "+" + getGitHash(), location

    replaceToken '@description@', project.descriptions, location
    replaceToken '@url@', project.url, location
}

jar {
    manifest {
        attributes  'Implementation-Title': project.name,
                'Implementation-Version': version,
                'SpongeAPI-Version': project.spongeapi,
                'Git-Hash': getGitHash()
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

def private String getGitHash() {
    def process = 'git rev-parse --short HEAD'.execute();
    process.waitFor();
    return process.exitValue() ? 'unknown' : process.text.trim();
}

